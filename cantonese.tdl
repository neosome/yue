;;; -*- Mode: TDL; Coding: utf-8 -*-
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Grammar of Cantonese
;;; created at:
;;;     Tue Jan 10 14:45:21 UTC 2023
;;; based on Matrix customization system version of:
;;;     Fri 10 Jun 2022 12:58:58 AM UTC
;;;
;;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Type assigning empty mod list. Added to basic types for nouns, verbs and determiners.

non-mod-lex-item := lex-item &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < > ].

;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Matrix Type Addenda
;;;;;;;;;;;;;;;;;;;;;;;;;

basic-head-comp-phrase :+ [ SYNSEM [ LIGHT #light,
             LOCAL.CAT [ HC-LIGHT #light,
                         MC #mc ] ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.MC #mc ].

head :+
"""
PRON is used in NPs to show if something is a pronouns?
PRON is used in ClP to show if something has been explicitly counted
     CTD in the paper
"""
[ PRON bool,
  POSSESSOR poss,
  INIT bool ].

png :+ [ PER person ].

cat :+ [ POSSESSUM poss ].

poss := *top* &
  [ POSS-AGR png ].

basic-bare-np-phrase :+ [ SYNSEM.LOCAL.CAT [ VAL.SPEC < >,
                       HEAD #head,
                       POSSESSUM #possessum ],
    HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD #head,
                                POSSESSUM #possessum & nonpossessive ] ].

basic-head-1st-comp-phrase :+ [ SYNSEM.LOCAL.CAT.POSSESSUM #poss,
    HEAD-DTR.SYNSEM.LOCAL.CAT.POSSESSUM #poss ].

basic-head-opt-subj-phrase :+ [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

top-coord-rule :+ [ SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                       POSSESSUM #possessum ],
    RCOORD-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                                  POSSESSUM #possessum ],
    LCOORD-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                                  POSSESSUM #possessum ] ].

mid-coord-rule :+ [ SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                       POSSESSUM #possessum ],
    RCOORD-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                                  POSSESSUM #possessum ],
    LCOORD-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                                  POSSESSUM #possessum ] ].

bottom-coord-phrase :+ [ SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                       POSSESSUM #possessum ],
    NONCONJ-DTR.SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR #possessor,
                                   POSSESSUM #possessum ] ].

;;;;;;;;;;;;;;
;;; Features
;;;;;;;;;;;;;;

;;; Person

person := *top*.
1st := person.
2nd := person.
3rd := person.

;;; Possession

possession := *top*.
possessive := poss.  ; supertype for possessive features
nonpossessive := poss.  ; type for nonpossessive
possessor := possessive.
possessum := possessive.
possessive-1 := possessive.  ; supertype for strategy 1
possessor-1 := possessor & possessive-1.
possessum-1 := possessum & possessive-1.

;;; Possession

possessive-2 := possessive.  ; supertype for strategy 2
possessor-2 := possessor & possessive-2.
possessum-2 := possessum & possessive-2.

;;; sub-part-of-speech: 

common := noun. 
proper := noun.
kin-or-role := noun. 
cls-or-det := head.
cls := cls-or-det.
det := cls-or-det & +do & +dm & +cd & +pd & +rd & +jd & +vd & +nd. 


kin := kin-or-role.
role := kin-or-role. 





;;;;;;;;;;;;;;;;;;;
;;; Lexical Types
;;;;;;;;;;;;;;;;;;;

;;; Nouns

wh-pronoun-noun-lex := basic-wh-word-lex & norm-hook-lex-item & basic-icons-lex-item & non-mod-lex-item & zero-arg-que &
  [ SYNSEM [ LOCAL [ CAT [ HEAD noun,
                           VAL [ SPR < >,
                                 SUBJ < >,
                                 COMPS < >,
                                 SPEC < > ] ],
                     CONT [ RELS.LIST < [ LBL #larg,
                                          ARG0 ref-ind & #arg0 ],
                                        quant-relation &
                                        [ PRED "which_q_rel",
                                          ARG0 #arg0,
                                          RSTR #harg ] >,
                            HCONS.LIST < [ HARG #harg,
                                           LARG #larg ] > ] ],
             NON-LOCAL.QUE.LIST < #arg0 > ] ].

noun-lex := basic-noun-lex & basic-non-wh-word-lex & non-local-none-lex-item & no-hcons-lex-item & non-mod-lex-item &
  [ ARG-ST < #spr >,
    SYNSEM.LOCAL.CAT [ VAL [ SPR < #spr &
                                   [ LOCAL.CAT [ HEAD cls, 
                                   VAL.SPR <> ] ] >,
                             COMPS < >,
                             SUBJ < >,
                             SPEC < > ],
                       HEAD.POSSESSOR nonpossessive,
                       POSSESSUM nonpossessive ] ].

no-spr-noun-lex := noun-lex &
  [ SYNSEM.LOCAL.CAT.VAL.SPR < [ OPT + ] > ].

; Nouns which cannot take specifiers mark their SPR requirement
; as OPT +.  Making the non-head daughter OPT - in this rule
; keeps such nouns out.
; 
; Rules for building NPs.  Note that the Matrix uses SPR for
; the specifier of nouns and SUBJ for the subject (specifier) of verbs.

;spec-head-phrase := basic-head-spec-phrase-super & head-compositional & head-final &

spec-head-phrase := basic-head-spec-phrase & head-final &
"""
The specifier cannot be a classifier; we do this to block the spec-head rule to apply with the numeral-cl as the specifier and N as the head. 
"""
  [ NON-HEAD-DTR.SYNSEM [OPT bool, 
                         LOCAL.CAT.HEAD +vjrpcdmo],
    SYNSEM.LOCAL.CONT.HOOK.XARG #x, 
    HEAD-DTR.SYNSEM.LOCAL.CONT.HOOK.XARG #x].

common-noun-lex := noun-lex &
[ SYNSEM.LOCAL.CAT.HEAD common ].

pronoun-noun-lex := no-spr-noun-lex &
  [ SYNSEM.LOCAL.CAT.HEAD.PRON + ].

proper-noun-lex := noun-lex &
[ SYNSEM.LOCAL.CAT.HEAD proper].


interrogative_pronoun-noun-lex := wh-pronoun-noun-lex.

kin-noun-lex := noun-lex & 
 [ SYNSEM.LOCAL.CAT.HEAD kin ].
 
role-noun-lex :=noun-lex &
 [ SYNSEM.LOCAL.CAT.HEAD role ].

;;; ::d rule 
;;; FCB FIXME -- CONT.HOOK #hdhook ???
cl-head-phrase-super := binary-nonloc-phrase & phrasal &
              binary-headed-phrase & head-compositional &
  [ INFLECTED infl-satisfied,
    SYNSEM [ LOCAL.CAT [ WH [ OR < #or1, #or2 > ],
                         VAL [ SUBJ #subj,
                               COMPS #spcomps,
                               SPR  < [ LOCAL.CAT.HEAD det &
			                               [ PRON #pron ] ] >,
			       SPEC #spec ],
			 POSTHEAD #ph,
			 HC-LIGHT - ],
              MODIFIED #modif ],
    HEAD-DTR [ INFLECTED infl-satisfied,
               SYNSEM [ LOCAL [ CAT [ WH #or1,
                                      HEAD #head,
                                      VAL [ SUBJ #subj,
                                            COMPS olist & #comps,
                                            SPR < #synsem  >, 
                                            SPEC #spec ],
                                      POSTHEAD #ph ],
                                CONT.HOOK [LTOP #h, index #i ]],
                        MODIFIED #hmodif ] ],
    NON-HEAD-DTR.SYNSEM #synsem &
     [ LOCAL [ CAT [ WH #or2,
		     HEAD.PRON #pron,
		     VAL [ SPR < >,
			   SPEC < [ LOCAL [ CAT [ HEAD #head,
						  VAL.COMPS #comps ],
					    CONT.HOOK #hdhook ],
				    MODIFIED #hmodif ] > ,
			   COMPS #spcomps ] ], 
	       CONT.HOOK [LTOP #h, XARG #i ] ] ,
       MODIFIED #modif ],
    C-CONT [ RELS.LIST < >, HCONS.LIST < >, ICONS.LIST < > ] ].

cl-head-phrase := cl-head-phrase-super & head-final &
  [ NON-HEAD-DTR.SYNSEM [LOCAL [ CAT.HEAD cls,
  			         CONT.HOOK #hook ],
                         OPT - ],
    C-CONT.HOOK #hook ].


;;; Verbs

verb-lex := basic-verb-lex & non-mod-lex-item & basic-non-wh-word-lex &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPEC < >,
                             SUBJ < #subj > ],
                   CONT.HOOK.XARG #xarg ],
    ARG-ST.FIRST #subj &
                 [ LOCAL [ CAT cat-sat &
                               [ VAL [ SPR < >,
                                       COMPS < > ] ],
                           CONT.HOOK.INDEX #xarg ] ] ].

intransitive-verb-lex := verb-lex & intransitive-lex-item &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < >,
    ARG-ST.FIRST.LOCAL.CAT.HEAD noun ].

transitive-verb-lex := verb-lex & transitive-lex-item &
  [ ARG-ST < [ LOCAL.CAT.HEAD noun ],
             #comps &
             [ LOCAL.CAT cat-sat &
                         [ VAL [ SPR < >,
                                 COMPS < > ],
                           HEAD noun ] ] >,
    SYNSEM.LOCAL.CAT [ VAL.COMPS < #comps >,
                       HEAD.INIT + ] ].

simple_intransitive-verb-lex := intransitive-verb-lex.

simple_transitive-verb-lex := transitive-verb-lex &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.OPT + ].

strict_transitive-verb-lex := transitive-verb-lex &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.OPT - ].

copula-verb-lex := simple_transitive-verb-lex.

no-obj-drop-verb-lex := transitive-verb-lex &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS.FIRST.OPT - ].

obj-drop-verb-lex := transitive-verb-lex.


;;; Verbal particles (marker)

marker-or-punct := head & 
 [ MOD <> ].
marker := marker-or-punct.
punct := marker-or-punct.


basic-head-marker-phrase := binary-nonloc-phrase & binary-headed-phrase & head-compositional &
"""
marker's comps = head
non-marker daughter = head

"""
  [SYNSEM.LOCAL.CAT.VAL #val,
   HEAD-DTR.SYNSEM canonical-synsem & #synsem & 
  	[LOCAL.CAT.VAL #val],
    NON-HEAD-DTR.SYNSEM [ LOCAL.CAT [ VAL [ SUBJ < >,
                                        SPR < >,
                                        SPEC < >,
                                        COMPS < #synsem > ]]],
       
    C-CONT [ RELS.LIST < >,
       HCONS.LIST < >,
       ICONS.LIST < > ] ].



head-marker-phrase := basic-head-marker-phrase & head-initial &
 [ MARKER-DTR.SYNSEM.LOCAL.CAT.HEAD marker ]. 
 
  
no-aspect := aspect.
has-aspect := aspect.
perfective-or-experential  := has-aspect.
perfective  := perfective-or-experential.
experential  := perfective-or-experential.
progressive := has-aspect.
continuous   := has-aspect.
tentative  := has-aspect. 
habitual  := has-aspect. 
experiential  := has-aspect. 

verbal-marker := raise-sem-lex-item & no-icons-lex-item &
"""

This is used by 'comp-marker' to join aspect markers to verbs (suchas  as 咗) and sentence final particles to sentences (such as ).

Verbal markers  take a complement  (previously +vp, +vj in zhong)
It was AUX- in zhong. 

"""
  [ SYNSEM [ LOCAL [ CAT [ HEAD marker & [ MOD <> ],
			   VAL [ SUBJ < >,
			   COMPS < [ LOCAL.CAT.HEAD +vj ] >,
				 SPEC < >,
				 SPR < > ],
			   POSTHEAD + ] ],
	     NON-LOCAL [ SLASH.list  < >,
                         REL.list < >,
                         QUE.list < > ] ] ].

aspect-marker := verbal-marker &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < lex-synsem &
                                      [ MODIFIED notmod ] >].


sentence-marker  := verbal-marker &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < phr-synsem  &
				      [ LOCAL.CAT cat-sat ] >].


pfv-marker-lex := aspect-marker &
"""
This is used for 咗, to mark perfective aspect.

<ex> 我 食 咗 個 蘋果 
<ex> 我 食 住 先 


"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT perfective ] > ] ].


exp-marker-lex := aspect-marker &
"""
This is used for 過, to mark experiential aspect.

<ex> 我 食 過 蘋果 



"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT perfective ] > ] ].


cont-marker-lex := aspect-marker &
"""
This is used for 住, to mark continous aspect.

<ex> 你 坐 住 先


"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT continuous ] > ] ].
					    
prog-marker-lex := aspect-marker &
"""
This is used for 緊, to mark progressive aspect.

<ex> 我 食 緊 個 蘋果
<ex> 我 食 緊 蘋果
<nex> 你 死 緊 

"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT progressive ] > ] ].
					    
ten-marker-lex := aspect-marker &
"""
This is used for 吓, to mark tentative aspect.

<ex> 你 食 吓 

<nex> 你 死 吓 

"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT tentative ] > ] ].
					    
hab-marker-lex := aspect-marker &
"""
This is used for 開／慣, to mark perfective aspect.

<ex> 你 食 開 蘋果


"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT habitual ] > ] ].
 
;;; Sentence final particle

sfp-q-lex := sentence-marker &
"""

Sentence final particle for polar questions

<ex> 你 食 嗎 
<ex> 你 食 乜嘢 嗎 
<nex> 邊個 食 嗎 

Forces indefinite reading of interrogative
FIXME block reapplication
FIXME block punctuation
FIXME merge with complementizer :-)
"""
  [ SYNSEM [ LOCAL [ CAT.VAL.COMPS < [ NON-LOCAL.QUE.LIST < >  ] >,
		     CONT.HOOK.INDEX.SF ques ] ] ].




;;; Complementizers

; We treat question particles as complementizers.
; Here is the lexical type for complementizers.

complementizer-lex-item := raise-sem-lex-item & non-local-none-lex-item & basic-icons-lex-item &
  [ SYNSEM.LOCAL.CAT [ HEAD comp &
                            [ MOD < > ],
                       VAL [ SPR < >,
                             SPEC < >,
                             SUBJ < >,
                             COMPS < #comp > ] ],
    ARG-ST < #comp &
             [ LOCAL.CAT [ HEAD verb,
                           VAL [ SUBJ < >,
                                 COMPS < > ] ] ] > ].

; Subtype for question particles. Constrains SF to ques.

qpart-lex-item := complementizer-lex-item &
  [ SYNSEM.LOCAL.CONT.HOOK.INDEX.SF ques ].

q-particle1-lex := qpart-lex-item &
  [ SYNSEM.LOCAL.CAT [ MC #mc,
                       VAL.COMPS.FIRST.LOCAL.CAT [ MC #mc & +,
                                                   WH.BOOL - ] ] ].

;;; Others

;;; Determiners
;;; SPEC is non-empty, and already specified by basic-determiner-lex.

determiner-lex := basic-determiner-lex & basic-non-wh-word-lex & norm-zero-arg & non-mod-lex-item &
  [ SYNSEM.LOCAL.CAT [ VAL [ SPR < >,
                             COMPS <  >,
                             SUBJ < > ],
                       HEAD.INIT + ] ].

demonstrative-determiner-lex := determiner-lex & no-icons-lex-item &
  [ SYNSEM.L-QUE -,
    SYNSEM.LKEYS.KEYREL.ARG0 [COG-ST activ+fam ] ].
  
;;; Classifiers

classifier-lex := norm-sem-lex-item & non-local-none-lex-item & non-mod-lex-item & no-hcons-lex-item &
"""
<ex> 我 食 一 個 蘋果 。
<ex> 明恩 食 呢 一 個 蘋果 。
<ex> 明恩 食 一 個 蘋果
<ex> 明恩 食 個 蘋果
<ex> 明恩 食 呢 個 蘋果
<ex> 明恩 食 蘋果
""" 
  [ SYNSEM [ LOCAL  [ CAT [ VAL [ SPR < [ LOCAL.CAT.HEAD num, 
					  OPT + ] >,
				  COMPS < >,
				  SUBJ < > ],
			    HEAD cls & 
				[INIT +] ],
                    CONT  [ HOOK.XARG #arg,
                    	    ICONS.LIST < > ] ], 
              LKEYS.KEYREL arg1-ev-relation &
                           [ ARG1 ref-ind & #arg ] ]].



;;; Numerals

card_rel := norm_rel.

carg-relation := arg0-relation &
  [ CARG string ].
    
card-relation := carg-relation & arg1-ev-relation &
	[ PRED card_rel ]. 
	
named-relation := noun-relation & carg-relation &
  [ PRED named_rel ].
	
numeral-super-lex := single-rel-lex-item & basic-icons-lex-item & 
"""
This is the main type for basic Numerals.
They have [HEAD num]. And the special head-feature [CARDINAL +].

Their semantics is somewhat special, using CARG in a similar way as named relations.
We want: the ARG0 of the thing it will specify over (SPEC) to be the same as ARG1 on the relation it introduces.
 
"""
  [ SYNSEM [ LOCAL [ CAT [ VAL [ COMPS < >,
				 SUBJ < >,
				 SPR < >,
				 SPEC < [ LOCAL.CONT.HOOK [
				 LTOP #lbl, INDEX #arg1 ]] > ],
			   HEAD num & [ MOD < > ] ],
		     CONT [ HOOK [ LTOP #lbl ],
			    RELS <! #rel !>,
			    HCONS <! !> ] ],
	     LKEYS.KEYREL card-relation & #rel & [ LBL #lbl,
						   ARG1 #arg1 ] ] ].

numeral-lex :=  numeral-super-lex & non-local-none-lex-item &
[ SYNSEM.LOCAL.CAT.VAL.SPEC <  [ LOCAL.CAT.HEAD [PRON + ] ] > ].

;numeral-que-lex = numeral-super-lex & ...   
; for 幾多

;;; Adverbs

two-rel-adposition-lex := basic-icons-lex-item & non-local-none-lex-item &
  [ SYNSEM [ LOCAL [ CAT [ HEAD adp,
                           VAL.COMPS < [ LOCAL [ CAT cat-sat,
                                                 CONT.HOOK #hook &
                                                           [ INDEX #ind,
                                                             ICONS-KEY.IARG1 #clause ] ] ] > ],
                     CONT.HOOK #hook &
                               [ CLAUSE-KEY #clause ] ],
             LKEYS.KEYREL arg12-ev-relation &
                          [ ARG2 #ind ] ] ].

possessor-adp-lex-1 := two-rel-adposition-lex & basic-intersective-mod-lex &
"""
This is the possesive marker, it takes a complement on the left (the possesor: ARG2) and modifies the possessee (ARG1) on the right.
<ex> 我 嘅 朋友 鍾意 大笨象 
"""
  [ SYNSEM.LOCAL [ CAT [ POSTHEAD -,
                         VAL [ SPEC < >,
                               SUBJ < >,
                               SPR < >,
                               COMPS.FIRST [ OPT -,
                                             LOCAL [ CAT [ HEAD noun,
                                                           VAL.SPR < > ],
                                                     CONT.HOOK.INDEX #possessor ] ] ],
                         HEAD [ INIT -,
                                POSSESSOR possessor-1,
                                MOD.FIRST.LOCAL [ CAT [ HEAD noun &
                                                             [ PRON - ] ],
                                                  CONT.HOOK [ INDEX #possessum,
                                                              LTOP #lbl ] ] ] ],
                   CONT [ ICONS.LIST < >,
                          HCONS.LIST < >,
                          RELS.LIST < arg12-ev-relation &
                                      [ PRED "poss_p_rel",
                                        LBL #lbl,
                                        ARG1 #possessum,
                                        ARG2 #possessor ] > ] ] ].

possessor-adp-NP-lex := possessor-adp-lex-1 &
"""
External possessive modificiation

<ex> 食 明恩 嘅 呢 個 蘋果
食 [明恩 嘅] [呢 個 蘋果]

maybe one reading of
<ex> 食 呢 個 明恩 嘅 蘋果 

where apple is a bare NP: 食 [ [呢 個 明恩] 嘅 ] 蘋果 

<nex> 食 呢 明恩 嘅 個 蘋果

"""
  [ SYNSEM.LOCAL.CAT.HEAD.MOD.FIRST.LOCAL.CAT.VAL.SPR <> ].


possessor-adp-N-lex  := possessor-adp-lex-1 &
"""
<ex> 食 呢 個 明恩 嘅 蘋果 
where apple is a bare N:  食  呢 個 [ 明恩 嘅 ] 蘋果 

<ex> 食 一 個 呢 個 明恩 嘅 蘋果 

<nex> 食 呢 明恩 嘅 個 蘋果
Blocked as the clp-N [個 蘋果] is looking for a a determiner
"""
  [ SYNSEM.LOCAL.CAT.HEAD.MOD.FIRST.LOCAL.CAT.VAL.SPR < [ LOCAL.CAT.HEAD cls ] > ].



basic-adposition-lex :+ [ SYNSEM.LOCAL.CAT [ HEAD.POSSESSOR nonpossessive,
                       POSSESSUM nonpossessive ] ].

;Constraint on MC used to be part of matrix.tdl
;;it applies to all wo implementations, except for v2

;;;;;;;;;;;;;;;;;;;
;;; Lexical Rules
;;;;;;;;;;;;;;;;;;;

;;; Lexical rule types

;;;;;;;;;;;;;;;;;;;
;;; Phrasal Types
;;;;;;;;;;;;;;;;;;;

;Constraint on MC used to be part of matrix.tdl
;;it applies to all wo implementations, except for v2

; The head of head-comp-phrase can't be: ['comp' or 'adp']

head-comp-phrase := basic-head-1st-comp-phrase & head-initial &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.INIT +,
    SYNSEM.LOCAL.CAT.HEAD +nvjrdmo ].

subj-head-phrase := decl-head-subj-phrase & head-final &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

; comp-head-phrase requires things that are [ HEAD comp or adp ].

comp-head-phrase := basic-head-1st-comp-phrase & head-final &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.INIT -,
    SYNSEM.LOCAL.CAT.HEAD +pc ].

possessum-mod-rule := basic-head-mod-phrase-simple &
  [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.MOD.FIRST.LOCAL.CAT.POSSESSUM #poss,
    HEAD-DTR.SYNSEM.LOCAL.CAT.POSSESSUM #poss ].

adj-head-int-phrase :+ possessum-mod-rule.

poss-phrase-2 := head-final & binary-nonloc-phrase & 
"""
The rule takes two nouns, both are not 'specified'. The left-hand daughter
must be a word; the right-hand daughter must be a kin, both will be made definite.

<ex> 明恩 細妹 笑
<ex> 我 細佬 鍾意 大笨象
<ex> 我 老師 笑      

<nex> 明恩 我 笑
<nex> 我 嘅 朋友 細妹 笑

to-do: currently, the righ-head daughter must be kin, but we should allow role nouns, like 老師.
"""
  [ NON-HEAD-DTR word & [SYNSEM.LOCAL [ CAT [ POSSESSUM nonpossessive,
                                      HEAD noun &
                                           [ POSSESSOR nonpossessive ],
                                      VAL.SPR < unexpressed &
                                              [ LOCAL.CAT.HEAD +nd,
                                                OPT + ] > ],
                                CONT.HOOK [ LTOP #possessor_lbl,
                                            INDEX #possessor ] ] ],
    SYNSEM.LOCAL.CAT [ HEAD kin-or-role  & #head,
                       VAL [ COMPS < >,
                             SUBJ < >,
                             SPEC < >,
                             SPR < > ] ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ POSSESSUM nonpossessive,
                                  HEAD #head & noun &
                                       [ POSSESSOR nonpossessive,
                                         PRON - ],
                                  VAL.SPR < [ ] > ],
                            CONT.HOOK #hook &
                                      [ LTOP #possessum_lbl,
                                        INDEX #possessum &
                                              [ COG-ST uniq-id ] ] ],
    C-CONT [ HOOK #hook &
                  [ INDEX #possessum ],
             ICONS.LIST < >,
             RELS.LIST < arg12-ev-relation &
                         [ PRED "poss_p_rel",
                           LBL #lbl,
                           ARG1 #possessum,
                           ARG2 #possessor ],
                         quant-relation &
                         [ PRED "def_q_rel",
                           ARG0 #possessum,
                           RSTR #possessum_harg ], 
                         quant-relation &
                         [ PRED "def_q_rel",
                           ARG0 #possessor,
                           RSTR #possessor_harg ] >,
             HCONS.LIST < qeq &
                          [ HARG #possessor_harg,
                            LARG #possessor_lbl ], 
                          qeq &
                          [ HARG #possessum_harg,
                            LARG #possessum_lbl ] > ] ].

;Constraint on MC used to be part of matrix.tdl
;;it applies to all wo implementations, except for v2

; Bare NP phrase.  Consider modifying the PRED value of the quantifier relation
; introduced to match the semantic effect of bare NPs in your language.

basic-bare-np-phrase := head-only & unary-nonloc-phrase &
  [ SYNSEM [ LOCAL.CAT [ WH #wh,
                       VAL [ SPR < >,
                           SUBJ < >,
                           COMPS < >,
			   SPEC #spec ] ] ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ WH #wh,
                                  HEAD noun,
                                  VAL [ SPR < unexpressed &
                                              [ LOCAL.CAT.HEAD cls-or-det,
                                                OPT + ] >,
                                        SUBJ < >,
                                        COMPS < >, SPEC #spec ] ],
                            CONT.HOOK [ INDEX #index,
                                        LTOP #larg ] ],
    C-CONT [ RELS.LIST < quant-relation &
                     [ ARG0 #index,
                       RSTR #harg ] >,
             HCONS.LIST < qeq &
                      [ HARG #harg,
                        LARG #larg ] >,
             ICONS.LIST < >,
             HOOK [ INDEX #index ] ] ].

bare-np-phrase-super := basic-bare-np-phrase &
  [ C-CONT.RELS.LIST < [ PRED "exist_q_rel"]  > ].

bare-np-phrase := bare-np-phrase-super &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SPR < [ LOCAL.CAT.HEAD.PRON + ] >,
    C-CONT.RELS.LIST < [ ARG0 [ COG-ST type-id  ] ] > ].

bare-cl-np-phrase := bare-np-phrase-super &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SPR < [ LOCAL.CAT.HEAD det & [PRON - ] ] >,
    C-CONT.RELS.LIST < [ ARG0 [ COG-ST fam-or-less  ] ] > ].


;;; bare-clp-phrase

bare-clp-phrase := head-only & unary-nonloc-phrase & 
  [ SYNSEM [ LOCAL [ CAT [ WH #wh,
                           VAL [ SPR < >,
                                 SUBJ < >,
                                 COMPS < >,
				 SPEC #spec &
				 < [ LOCAL.CONT.HOOK.INDEX #nindex ] >] ],
                     CONT.HOOK [ INDEX #nindex,
                                 LTOP #lbl, 
                                 XARG #xarg ] ] ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ WH #wh,
                                  HEAD cls & [ PRON - ],
                                  VAL [ SPR < unexpressed &
                                              [ LOCAL.CAT.HEAD num,
                                                OPT bool ] >,
                                        SUBJ < >,
                                        COMPS < >,
					SPEC #spec ] ],
                            CONT.HOOK [ INDEX #index,
                                        LTOP #lbl, 
                                 	XARG #xarg] ],
    C-CONT [ RELS.LIST < card-relation &
                     [  LBL #lbl,
                        ARG1 #index, 
                        CARG "1" ] >,
             HCONS.LIST < >,
             ICONS.LIST < > ]].


;;; Wh-question-related phrasal types

; In-situ interrogative clause.

;;;;;;;;;;;;;;;;;;
;;; Coordination
;;;;;;;;;;;;;;;;;;

;;; Coordination Strategy 1

np1-top-coord-rule := basic-np-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

np1-mid-coord-rule := basic-np-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

np1-bottom-coord-rule := conj-first-bottom-coord-rule & np-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

;;; Coordination Strategy 1

vp1-top-coord-rule := basic-vp-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

vp1-mid-coord-rule := basic-vp-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

vp1-bottom-coord-rule := conj-first-bottom-coord-rule & vp-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

insitu-int-cl := interrogative-clause & head-only &
  [ C-CONT [ RELS.LIST < >,
             HCONS.LIST < > ],
    HEAD-DTR.SYNSEM [ LOCAL.CAT [ HEAD verb,
                                  VAL #val &
                                      [ SUBJ < >,
                                        COMPS < > ] ],
                      NON-LOCAL [ SLASH.LIST < >,
                                  REL.LIST < >,
                                  QUE.LIST.FIRST ref-ind ] ],
    SYNSEM [ MODIFIED hasmod,
             NON-LOCAL [ SLASH.LIST < >,
                         QUE.LIST < >,
                         REL.LIST < > ],
             LOCAL.CAT [ VAL #val,
                         MC bool,
                         WH.BOOL + ] ] ].
                         
                         
;; Type for negative adverbs.

;; SSH 2015-01-26: Why is [ MC + ] needed?
;; ZZF 2016-11-4: negation adverb also modifies adj，and it's scopal modifier, not SPR (therefore SPEC < >). Only degree adverbs are specifiers.
;; ZZF 2016-12-8: taking out "LIGHT +," from MOD, to allow "不 常 来", "不 太 喜欢"
neg-adv-lex := basic-scopal-adv-lex & non-local-none-lex-item &
"""
Tentatively we are setting the MOD's COMP value to empty (< >) to avoid ambiguity on the level of attachment. 

FIXME:
For educational purposes, we want to block 不 before degree specifiers (e.g., 他 不 很 高 。). 
But it is not clear whether this is problematic in casual speech -- we might not want to block it altogether.
We experimented with setting the SPR to  <[ ]> (a list of length one) to make 不 select the adjective before the degree specifier. But this does not work:
Verbs come with empty specifiers (SPR < >). Adjectives come looking for a optional single specifier (SPR <[ ]>) that is their degree specifier.
Therefore, we can't use SPR to block the order in which 不 appears.

<ex>??? 他 不 很 高 。/ *佢唔好高。
<ex>他 不 高 。/ 佢唔高。
<ex>他 很 不 高 。/* 佢好唔高。
<ex>他 很 不 好吃 。
<ex>你 不 知道 。/佢唔知道。


Let's say: Thre is a  the scopal BU could be used for 1,2,5 and then we could ave a lexical BU
Lexical BU would be a degree specifier...  and scopal BU does not take degree spc.

EVERYONE NOT HAPPY.  >>> 

If we have a non-scopal that REQUIRES a degree-SPR.  +  a scopal that IS a degree-SPR.
maybe a specl head-spr rule : the normal rule would be intersective; This would have to ALSO allow to be specified FURTHER 
the special one would be scopal and allow BU

"""
  [ SYNSEM.LOCAL.CAT [ VAL [ COMPS < >,
			     SPEC < > ],
		       HEAD.MOD < [ LOCAL.CAT [ HEAD +vj,
						VAL [SUBJ cons,
						     COMPS < >] ] ] > ] ].


;;redefining the types in matrix.tdl. Taken out raise-index-mod-lex-item from basic-adverb-lex and put it in basic-scopal-adverb-lex. 
basic-adv-lex :=  single-rel-lex-item & basic-icons-lex-item &
  [ SYNSEM [LOCAL.CAT.HEAD adv,
	     LKEYS.KEYREL event-relation ]].

;;zzf and fb 2016-11-7  
;;raise-index-mod-lex-item its HOOK.INDEX is the same as that of the thing it modifies
;;removing SPR as scopal adverbs don't seem to take degree specifiers.
basic-scopal-adv-lex := basic-adv-lex & scopal-mod-lex & raise-index-mod-lex-item &
  [ SYNSEM [ LOCAL.CAT.VAL [ SUBJ < >,
                             SPR < > ] ] ].
;;                             SPR < [ LOCAL.CAT [ HEAD adv,
;;                                                 VAL.SPEC < [ LOCAL.CAT.HEAD adv ] > ] ] ;;> ] ] ].

;;;Adjectives

basic-pred-adj-lex := basic-intersective-adjective-lex & no-hcons-lex-item.

pred-adj-lex := basic-pred-adj-lex &
"""
This is the main type for predicative adjectives. In principle, these ought to be able to server as predicates without a degree specifier (e.g. 很) but this construction is marked as comparative/constrastive. E.g.
她 漂亮。  She is more beautiful.



ZZF FB LMC 2016-2-24 SPR OPT changed from - to bool to allow adj predicate without specifier (adv) 姑娘　漂亮 ;
2016-4-20 taking out linking of NON-LOCAL.SLASH with that of its SUBJ

2021.01.27 -- LMC and FCB put back the linking of NON-LOCAL.SLASH with its SUBJ. We need to do a regression test. This was to remove bad analysis with optional subjects in relative clauses of Adjectives.
"""
  [ SYNSEM [ LOCAL [ CAT [ HEAD [ MOD <> ],
			   VAL [ SUBJ < [LOCAL [CAT [HEAD noun, 
						     VAL.SPR < > ],
						CONT.HOOK.INDEX #index ],
					 NON-LOCAL.SLASH  #slash ] >,
				 COMPS < > ] ] ],
	     NON-LOCAL.SLASH  #slash,
	     LKEYS.KEYREL.ARG1 #index ] ].


;pred-adj-nospr-lex := pred-adj-lex &
;  [ SYNSEM [ LOCAL [ CAT [ VAL [ SPR < > ] ] ] ] ].
		      
pred-adj-optspr-lex := pred-adj-lex &
  [ SYNSEM [ LOCAL [ CAT [ VAL [ SPR < [ LOCAL [ CAT [ HEAD adv,
						       VAL.SPEC < [ LOCAL.CAT.HEAD adj ] > ] ],
					 OPT bool ] > ] ] ] ] ].

pred-adj-spr-lex := pred-adj-optspr-lex &
  [ SYNSEM [ LOCAL [ CAT [ VAL [ SPR < [ OPT - ] > ] ] ] ] ].

  
				   


